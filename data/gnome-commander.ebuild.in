# Copyright 1999-2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

GNOME2_LA_PUNT="yes"

inherit gnome2 optfeature toolchain-funcs

DESCRIPTION="A graphical, full featured, twin-panel file manager"
HOMEPAGE="https://gcmd.github.io/"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="chm exif gsf pdf samba taglib test +unique"
RESTRICT="!test? ( test )"

RDEPEND="
	app-text/yelp-tools
	>=dev-libs/glib-@GLIB_REQ@:2
	>=gnome-base/gnome-vfs-@GNOMEVFS_REQ@
	>=x11-libs/gtk+-@GTK_REQ@:2
	chm? ( dev-libs/chmlib )
	exif? ( >=media-gfx/exiv2-@EXIV2_REQ@ )
	gsf? ( >=gnome-extra/libgsf-@LIBGSF_REQ@ )
	pdf? ( >=app-text/poppler-@POPPLER_REQ@ )
	samba? ( >=gnome-base/gnome-vfs-@GNOMEVFS_REQ@[samba] )
	taglib? ( >=media-libs/taglib-@TAGLIB_REQ@ )
	unique? ( >=dev-libs/libunique-@UNIQUE_REQ@:1 )
"
BDEPEND="
	dev-util/gtk-doc-am
	sys-devel/gettext
	virtual/pkgconfig
"
DEPEND="
	${RDEPEND}
	test? ( >=dev-cpp/gtest-@GTEST_REQ@ )
"

pkg_pretend() {
	if tc-is-gcc && [[ $(gcc-major-version) -lt 8 ]]; then
		eerror "Compilation with gcc older than version 8 is not supported"
		die "GCC too old, please use gcc-8 or above"
	fi
}

src_configure() {
	gnome2_src_configure \
		--disable-static \
		$(use_with chm libchm) \
		$(use_with exif exiv2) \
		$(use_with gsf libgsf) \
		$(use_with pdf poppler) \
		$(use_with samba) \
		$(use_with taglib) \
		$(use_with unique)
}

pkg_postinst() {
	gnome2_pkg_postinst
	optfeature "synchronizing files and directories" dev-util/meld
	optfeature "viewing the documentation" gnome-extra/yelp
}
