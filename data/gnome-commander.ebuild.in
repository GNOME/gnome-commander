# Copyright 1999-2019 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=6

GNOME2_LA_PUNT="yes"

inherit gnome2 toolchain-funcs

DESCRIPTION="A graphical, full featured, twin-panel file manager"
HOMEPAGE="https://gcmd.github.io/"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="chm exif gsf pdf taglib samba test +unique"

RDEPEND="
	app-text/yelp-tools
	>=dev-libs/glib-@GLIB_REQ@:2
	unique? ( >=dev-libs/libunique-@UNIQUE_REQ@:1 )
	gnome-base/gnome-keyring
	>=gnome-base/gnome-vfs-@GNOMEVFS_REQ@
	>=gnome-base/libgnome-@GNOME_REQ@
	>=gnome-base/libgnomeui-@GNOMEUI_REQ@
	>=x11-libs/gtk+-@GTK_REQ@:2
	chm? ( dev-libs/chmlib )
	exif? ( >=media-gfx/exiv2-@EXIV2_REQ@ )
	gsf? ( >=gnome-extra/libgsf-@LIBGSF_REQ@ )
	samba? ( >=gnome-base/gnome-vfs-@GNOMEVFS_REQ@[samba] )
	pdf? ( >=app-text/poppler-@POPPLER_REQ@ )
	taglib? ( >=media-libs/taglib-@TAGLIB_REQ@ )
"
DEPEND="
	${RDEPEND}
	dev-util/gtk-doc-am
	sys-devel/gettext
	virtual/pkgconfig
	test? ( >=dev-cpp/gtest-@GTEST_REQ@ )
"

src_configure() {
	gnome2_src_configure \
		--disable-static \
		$(use_with chm libchm) \
		$(use_with exif exiv2) \
		$(use_with gsf libgsf) \
		$(use_with pdf poppler) \
		$(use_with samba) \
		$(use_with taglib) \
		$(use_with unique)
}

pkg_pretend() {
	if tc-is-gcc && [[ $(gcc-major-version) -lt 8 ]]; then
		eerror "Compilation with gcc older than version 8 is not supported"
		die "GCC too old, please use gcc-8 or above"
	fi
}

pkg_postinst() {
	gnome2_pkg_postinst
	has_version dev-util/meld || elog "You need dev-util/meld to synchronize files and directories."
	has_version gnome-extra/yelp || elog "You need gnome-extra/yelp to view the docs."
}
